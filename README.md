# ReportsGenerator

Projeto Elixir que extrai dados de um arquivo `.csv` que corresponde a um relat√≥rio de usu√°rios de um aplicativo de delivery, contendo os dados da quantidade e de qual alimento cada usu√°rio consumiu.

Os dados do arquivo ser√° extra√≠do, processado e avaliado conforme seu desempenho durante a execu√ß√£o que retornar√°:

- qual o usu√°rio que mais consumiu, e
- qual o alimento mais consumido.

Teremos duas formas de execu√ß√£o:

- um arquivo contendo 300 mil linhas de dados
- 3 arquivos contendo 100 mil linhas de dados cada

## Inicializa√ß√£o

Iniciando o projeto

```bash
mix new reports_generator
```

## Configura√ß√£o de Credo

Credo √© um analisador sint√°tico do c√≥digo, que nos ajuda a manter a boas de escrita do nosso c√≥digo. Para instalar, seguir as instru√ß√µes desse [repo](https://github.com/rrrene/credo).

Adicionar a depend√™ncia no arquivo `mix.exs` na parte de `deps`:

```elixir
defp deps do
  [
    {:credo, "~> 1.5", only: [:dev, :test], runtime: false}
  ]
end
```

Observe que o primeiro argumento da tupla √© o nome da lib `:credo` e o segundo `"~> 1.5"` √© a vers√£o, e a terceira parte n√£o costuma ter, mas no caso do Credo tem porque √© usado somente para ambiente de desenvolvimento `:dev` e de teste `:test` para checarmos nosso c√≥digo. Al√©m disso, `runtime: false` n√£o encapsula essa lib no bin√°rio que vai ser gerado para ser executado no nosso servidor.

No VSCode, as depend√™ncias j√° s√£o baixadas automaticamente. Mas √© poss√≠vel baix√°-las pelo terminal com o comando:

```bash
mix deps.get
```

Uma vez instalado o Credo, vamos dar o comando que vai gerar o arquivo `.credo.exs` para configur√°-lo (o que vai checar ou n√£o):

```bash
mix credo gen.config
```

Por exemplo, como n√£o vamos fazer a documenta√ß√£o dos m√≥dulos, vamos alterar na parte de `Readability Checks` essa parte:

```elixir
{Credo.Check.Readability.ModuleDoc, false},
```

Para rodar o Credo:

```bash
mix credo
```

E para pegar at√© o que est√° com `[priority: :low]` como nesse caso:

```elixir
{Credo.Check.Readability.MaxLineLength, [priority: :low, max_length: 120]},
```

usamos o comando do Credo mais severo:

```bash
mix credo --strict
```

E caso n√£o queira ficar rodando na linha de comando sempre, podemos baixar a extens√£o do VSCode [ElixirLinter](https://marketplace.visualstudio.com/items?itemName=iampeterbanjo.elixirlinter) e deixar a op√ß√£o `Use strict mode with Credo` habilitada.

## Lendo arquivo

Vamos criar a fun√ß√£o `build` que vai receber o nome do arquivo .csv como argumento dentro do nosso m√≥dulo `ReportsGenerator`

```elixir
defmodule ReportsGenerator do
  def build(filename) do
    file = File.read("reports/#{filename}")
  end
end
```

E rodar no `iex`

```bash
iex -S mix
```

Ao ler um arquivo existente:

```elixir
ReportsGenerator.build("report_test.csv")
#..> {:ok, "1,pizza,48\r\n2,a√ßa√≠,45\r\n3,hamb√∫rguer,31\r\n4,esfirra,42\r\n5,hamb√∫rguer,49\r\n6,esfirra,18\r\n7,pizza,27\r\n8,esfirra,25\r\n9,churrasco,24\r\n10,churrasco,36"}
```

E para um arquivo inexistente:

```elixir
ReportsGenerator.build("report_testq.csv")
#..> {:error, :enoent}
```

Para tratar ambas situa√ß√µes, podemos usar o [case](https://elixir-lang.org/getting-started/case-cond-and-if.html#case), no qual o `_ -> "caso qualquer"` seria o `default`, mas n√£o ser√° necess√°rio para esse caso especificamente:

```elixir
defmodule ReportsGenerator do
  def build(filename) do
    case File.read("reports/#{filename}") do
      {:ok, result} -> result
      {:error, reason} -> reason
      _ -> "caso qualquer"
    end
  end
end
```

## Pipe operator

[Pipe operator](https://elixir-lang.org/getting-started/enumerables-and-streams.html#the-pipe-operator) √© uma feature do Elixir que ilustra bem como funciona as opera√ß√µes como um ch√£o de f√°brica.

Por exemplo, ao tratar a string `" aAaaAaaa \n"`, podemos fazer em v√°rias etapas no `iex`:

```elixir
string = " aAaaAaaa \n"
#..> " aAaaAaaa \n"
string = String.trim(string)
#..> "aAaaAaaa"
string = String.downcase(string)
#..> "aaaaaaaa"
```

Ou poder√≠amos fazer em uma linha s√≥, por√©m n√£o fica t√£o leg√≠vel a concatena√ß√£o das fun√ß√µes:

```elixir
string = String.trim(String.downcase(" aAaaAaaa \n"))
#..> "aaaaaaaa"
```

O pipe operator pega o resultado da opera√ß√£o anterior e passa como primeiro argumento da fun√ß√£o seguinte. Ent√£o, fica mais elegante e leg√≠vel a concatena√ß√£o das fun√ß√µes:

```elixir
" aaaAaaaaAAAAaaaa \n" |> String.trim() |> String.downcase()
#..> "aaaaaaaaaaaaaaaa"
```

Refatorando o c√≥digo:

```elixir
defmodule ReportsGenerator do
  def build(filename) do
    "reports/#{filename}"
    |> File.read()
    |> handle_file()
  end

  def handle_file({:ok, file_content}), do: file_content
  def handle_file({:error, _reason}), do: "Error while opening file"
end
```

O primeiro argumento fica impl√≠cito na chamada da fun√ß√£o seguinte. Caso necessite passar outros argumentos para fun√ß√µes de maior aridade, ent√£o precisamos pass√°-los explicitamente:

```elixir
defmodule ReportsGenerator do
  def build(filename) do
    "reports/#{filename}"
    |> File.read()
    |> handle_file("lalala")
  end

  def handle_file({:ok, file_content}, msg), do: file_content <> msg
  def handle_file({:error, _reason}, msg), do: "Error while opening file" <> msg
end
```

Obs: a concatena√ß√£o de string √© por `<>`.

No `iex`:

```elixir
ReportsGenerator.build("report_test.csv")
#..> "1,pizza,48\r\n2,a√ßa√≠,45\r\n3,hamb√∫rguer,31\r\n4,esfirra,42\r\n5,hamb√∫rguer,49\r\n6,esfirra,18\r\n7,pizza,27\r\n8,esfirra,25\r\n9,churrasco,24\r\n10,churrasco,36lalala"

ReportsGenerator.build("report_test.csvs")
#..> "Error while opening filelalala"
```

## Leitura em stream

Ao inv√©s usar [File.read](https://hexdocs.pm/elixir/File.html#read/1), iremos usar o [File.stream!](https://hexdocs.pm/elixir/File.html#stream!/3).

```elixir
  def build(filename) do
    "reports/#{filename}"
    |> File.stream!()
    |> IO.inspect()
  end
```

Somente para vermos o que o `File.stream!` est√° retornando no `iex`:

```elixir
ReportsGenerator.build("report_test.csv")
#..> %File.Stream{
#..>   line_or_bytes: :line,
#..>   modes: [:raw, :read_ahead, :binary],
#..>   path: "reports/report_test.csv",
#..>   raw: true
#..> }
#..> %File.Stream{
#..>   line_or_bytes: :line,
#..>   modes: [:raw, :read_ahead, :binary],
#..>   path: "reports/report_test.csv",
#..>   raw: true
#..> }
```

Vemos que retorna uma [struct](https://elixir-lang.org/getting-started/structs.html#structs-are-bare-maps-underneath), que nada mais √© que um `Map` com um nome.

O `File.Stream` n√£o possui o conte√∫do do arquivo, nele temos os metadados como:

- `line_or_bytes` que √© como ele vai ler o arquivo
- `path` que √© o caminho do arquivo

Ele n√£o tem o conte√∫do em si do arquivo, pois √© uma fun√ß√£o com caracter√≠stica `laziness`, que s√≥ traz o conte√∫do quando quiser utilizar.

O `File.stream!` combinado com o `Enum.each` vai dar o efeito de: ler uma linha e executar uma opera√ß√£o. De forma que vai lendo o arquivo e executando linha a linha, sem a necessidade de carregar o conte√∫do inteiro do arquivo na mem√≥ria. E tem o `!` porque diferentemente do `File.read` que retorna tupla de `:ok` ou `:error` para ler o arquivo e retornar o conte√∫do, no `File.stream!` s√≥ vai ler o arquivo no futuro.

```elixir
defmodule ReportsGenerator do
  def build(filename) do
    "reports/#{filename}"
    |> File.stream!()
    |> Enum.each(fn elem -> IO.inspect(elem) end)
  end
end
```

Retornando assim:

```elixir
ReportsGenerator.build("report_test.csv")
#..> "1,pizza,48\n"
#..> "2,a√ßa√≠,45\n"
#..> "3,hamb√∫rguer,31\n"
#..> "4,esfirra,42\n"
#..> "5,hamb√∫rguer,49\n"
#..> "6,esfirra,18\n"
#..> "7,pizza,27\n"
#..> "8,esfirra,25\n"
#..> "9,churrasco,24\n"
#..> "10,churrasco,36"
#..> :ok
```

Ou seja, para cada linha do arquivo, foi executado um `IO.inspect()`. No final da opera√ß√£o, o `Enum.each()` retorna um `:ok` para dizer que a opera√ß√£o ocorreu com sucesso.

Para melhorar, vamos separar da linha `"1,pizza,48\n"` quem √© `id`, `produto`, `valor` e remover `\n`.

```elixir
defmodule ReportsGenerator do
  def build(filename) do
    "reports/#{filename}"
    |> File.stream!()
    |> Enum.map(fn line -> parse_line(line) end)
  end

  defp parse_line(line) do
    line
    |> String.trim()
    |> String.split(",")
  end
end
```

Ao rodar no `iex`, temos:

```elixir
ReportsGenerator.build("report_test.csv")
#..> [
#..>   ["1", "pizza", "48"],
#..>   ["2", "a√ßa√≠", "45"],
#..>   ["3", "hamb√∫rguer", "31"],
#..>   ["4", "esfirra", "42"],
#..>   ["5", "hamb√∫rguer", "49"],
#..>   ["6", "esfirra", "18"],
#..>   ["7", "pizza", "27"],
#..>   ["8", "esfirra", "25"],
#..>   ["9", "churrasco", "24"],
#..>   ["10", "churrasco", "36"]
#..> ]
```

Ent√£o vemos que o primeiro elemento da lista √© o `id`, o segundo √© o `produto` e o terceiro √© o `pre√ßo`. Mas vamos alterar o pre√ßo de string para valor

```elixir
  defp parse_line(line) do
    line
    |> String.trim()
    |> String.split(",")
    |> List.update_at(2, &String.to_integer/1)
  end
```

Retornando o terceiro elemento como `integer`:

```elixir
ReportsGenerator.build("report_test.csv")
#..> [
#..>   ["1", "pizza", 48],
#..>   ["2", "a√ßa√≠", 45],
#..>   ["3", "hamb√∫rguer", 31],
#..>   ["4", "esfirra", 42],
#..>   ["5", "hamb√∫rguer", 49],
#..>   ["6", "esfirra", 18],
#..>   ["7", "pizza", 27],
#..>   ["8", "esfirra", 25],
#..>   ["9", "churrasco", 24],
#..>   ["10", "churrasco", 36]
#..> ]
```

Ao criar uma fun√ß√£o an√¥nima, podemos usar a sintaxe:

```elixir
    |> Enum.map(fn line -> parse_line(line) end)
```

Ou de maneira impl√≠cita com o `&`:

```elixir
    |> Enum.map(&parse_line(&1))
```

No caso de uma fun√ß√£o de m√≥dulo externo, a fun√ß√£o an√¥nima expl√≠cita seria:

```elixir
    |> List.update_at(2, fn elem -> String.to_integer(elem) end)
```

E a fun√ß√£o impl√≠cita:

```elixir
    |> List.update_at(2, &String.to_integer/1)
```

## Parse do arquivo

Ao inv√©s de retornar uma lista de listas, seria melhor retornar um Map `%{id: valor}`, onde o `id` seria o `id` do usu√°rio e o `valor` seria a soma dos valores gastos pelo usu√°rio.

[Enum.reduce](https://hexdocs.pm/elixir/Enum.html#reduce/3)

```elixir
Enum.reduce([1,2,3,4], 0, fn elem, acc -> acc + elem end)
#..> 10
```

E vamos observar o que acontece em cada parte:

```elixir
Enum.reduce([1,2,3,4], %{}, fn elem, acc -> IO.inspect(elem); IO.inspect(acc); Map.put(acc, elem, elem) end)
#..> 1
#..> %{}
#..> 2
#..> %{1 => 1}
#..> 3
#..> %{1 => 1, 2 => 2}
#..> 4
#..> %{1 => 1, 2 => 2, 3 => 3}
#..> %{1 => 1, 2 => 2, 3 => 3, 4 => 4}
```

Vamos refatorar o m√©todo `build` para retornar um Map contendo o id do usu√°rio como chave e o price como valor.

```elixir
  def build(filename) do
    "reports/#{filename}"
    |> File.stream!()
    |> Enum.reduce(%{}, fn line, report ->
      [id, _food_name, price] = parse_line(line)
      Map.put(report, id, price)
    end)
  end
```

```elixir
ReportsGenerator.build("report_test.csv")
#..> %{
#..>   "1" => 48,
#..>   "10" => 36,
#..>   "2" => 45,
#..>   "3" => 31,
#..>   "4" => 42,
#..>   "5" => 49,
#..>   "6" => 18,
#..>   "7" => 27,
#..>   "8" => 25,
#..>   "9" => 24
#..> }
```

Mas nosso m√©todo ainda est√° retornando somente o valor da √∫ltimo registro do usu√°rio no arquivo csv por causa do `Map.put(report, id, price)`.

Para acessar o valor atual, acessar por `report[id]` e somar com o `price`, alterando ent√£o para `Map.put(report, id, report[id] + price)`. Mas isso gera uma exce√ß√£o na primeira vez que roda.

```elixir
ReportsGenerator.build("report_test.csv")
#..> ** (ArithmeticError) bad argument in arithmetic expression: nil + 48
#..>     :erlang.+(nil, 48)
#..>     (reports_generator 0.1.0) lib/reports_generator.ex:7: anonymous fn/2 in ReportsGenerator.build/1
#..>     (elixir 1.11.4) lib/enum.ex:3473: anonymous fn/3 in Enum.reduce/3
#..>     (elixir 1.11.4) lib/stream.ex:1449: Stream.do_element_resource/6
#..>     (elixir 1.11.4) lib/enum.ex:3473: Enum.reduce/3
```

Para contornar a soma de `nil` com um valor qualquer, podemos criar um Map acumulador inicial com essa estrutura:

```elixir
%{
  "1" => 0,
  "2" => 0,
  "3" => 0,
  "4" => 0,
  "5" => 0,
  "..." => 0,
  "30" => 0,
}
```

Pois temos 30 usu√°rios nesses arquivos.

[Enum.into](https://hexdocs.pm/elixir/Enum.html#into/3) √© uma fun√ß√£o onde podemos pegar uma cole√ß√£o e converter em outra.

Gerar um `range` pegando o valor inicial e at√© onde queremos ir

```elixir
Enum.map(1..30, fn elem -> elem end)
#..> [1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20, 21, 22, 23, 24, 25, 26, 27, 28, 29, 30]
```

Usando syntax sugar:

```elixir
Enum.into(1..30, %{}, &{&1, &1})
#..> %{
#..>   1 => 1,
#..>   2 => 2,
#..>   3 => 3,
#..>   ...
#..>   29 => 29,
#..>   30 => 30
#..> }
```

Ou, de maneira mais expl√≠cita:

```elixir
Enum.into(1..30, %{}, fn x -> {x, x} end)

#..>   1 => 1,
#..>   2 => 2,
#..>   3 => 3,
#..>   ...
#..>   29 => 29,
#..>   30 => 30
#..> }
```

Mas queremos a chave como string e o valor zerado:

```elixir
Enum.into(1..30, %{}, fn x -> {Integer.to_string(x), 0} end)
#..> %{
#..>   "1" => 0,
#..>   "10" => 0,
#..>   "11" => 0,
#..>   "12" => 0,
#..>   ...
#..> }
```

Ou, como sugar syntax:

```elixir
Enum.into(1..30, %{}, &{Integer.to_string(&1), 0})
```

Vamos criar uma fun√ß√£o `report_acc` que retorna esse `Enum.into` acima e refatorar o m√©todo `build` do nosso m√≥dulo:

```elixir
defmodule ReportsGenerator do
  def build(filename) do
    "reports/#{filename}"
    |> File.stream!()
    |> Enum.reduce(report_acc(), fn line, report ->
      [id, _food_name, price] = parse_line(line)
      Map.put(report, id, report[id] + price)
    end)
  end

  defp parse_line(line) do
    line
    |> String.trim()
    |> String.split(",")
    |> List.update_at(2, &String.to_integer/1)
  end

  defp report_acc, do: Enum.into(1..30, %{}, &{Integer.to_string(&1), 0})
end
```

E ao recompilar e executar:

```elixir
ReportsGenerator.build("report_complete.csv")
#..> %{
#..>   "1" => 278849,
#..>   "10" => 268317,
#..>   "11" => 268877,
#..>   "12" => 276306,
#..>   "13" => 282953,
#..>   ...
#..> }
```

## Extraindo o parser para um novo m√≥dulo

A responsabilidade de parse das linhas pode ficar em outro m√≥dulo. Criamos um novo arquivo dentro de `lib` chamado `parser.ex`. E para evitar de repetir o uso de `Enum` nos dois arquivos, vamos usar o m√≥dulo `Stream`. Assim como o `File.stream!()` √© _lazy_ na execu√ß√£o (s√≥ executa quando precisamos), o m√≥dulo [Stream](https://hexdocs.pm/elixir/Stream.html) tamb√©m √©. A gente monta um conjunto de opera√ß√µes que v√£o ser executadas, mas elas ser√£o de fato executadas quando a gente precisar dos valores.

```elixir
defmodule ReportsGenerator.Parser do
  def parse_file(filename) do
    "reports/#{filename}"
    |> File.stream!()
    |> Stream.map(fn line -> parse_line(line) end)
    |> Enum.each(fn elem -> IO.inspect(elem) end) # remover essa linha depois
  end

  defp parse_line(line) do
    line
    |> String.trim()
    |> String.split(",")
    |> List.update_at(2, &String.to_integer/1)
  end
end
```

Ao rodar no `iex`:

```elixir
ReportsGenerator.Parser.parse_file("report_test.csv")
#..> ["1", "pizza", 48]
#..> ["2", "a√ßa√≠", 45]
#..> ["3", "hamb√∫rguer", 31]
#..> ["4", "esfirra", 42]
#..> ["5", "hamb√∫rguer", 49]
#..> ["6", "esfirra", 18]
#..> ["7", "pizza", 27]
#..> ["8", "esfirra", 25]
#..> ["9", "churrasco", 24]
#..> ["10", "churrasco", 36]
#..> :ok
```

Refatorando agora no m√≥dulo `ReportsGenerator`:

```elixir
defmodule ReportsGenerator do
  def build(filename) do
    filename
    |> ReportsGenerator.Parser.parse_file()
    |> Enum.reduce(report_acc(), fn [id, _food_name, price], report ->
      Map.put(report, id, report[id] + price)
    end)
  end

  defp report_acc, do: Enum.into(1..30, %{}, &{Integer.to_string(&1), 0})
end
```

A primeira melhoria que podemos fazer √© quando usamos um m√≥dulo externo que tem um nome muito grande, podemos fazer um alias. E outra melhoria √© isolar a soma em uma fun√ß√£o.

```elixir
defmodule ReportsGenerator do
  alias ReportsGenerator.Parser

  def build(filename) do
    filename
    |> Parser.parse_file()
    |> Enum.reduce(report_acc(), fn line, report -> sum_values(line, report) end)
  end

  defp sum_values([id, _food_name, price], report), do: Map.put(report, id, report[id] + price)

  defp report_acc, do: Enum.into(1..30, %{}, &{Integer.to_string(&1), 0})
end
```

## Retornando o maior valor

Estamos retornando um relat√≥rio de quanto usu√°rio consumiu. Agora faremos uma fun√ß√£o que recebe esse `report` e retorne qual usu√°rio que mais consumiu.

O m√≥dulo `Enum` tem a fun√ß√£o [Enum.max_by()](https://hexdocs.pm/elixir/Enum.html#max_by/4) que vai retornar o √≠tem de maior valor de uma cole√ß√£o dada.

Como o `report` est√° vindo como um `Map` de chave e valor, e quero que retorne pelo maior `value`.

```elixir
  def fetch_higher_cost(report), do: Enum.max_by(report, fn {_key, value} -> value end)
```

Pelo `iex`, podemos usa o `pipe operator`:

```elixir
"report_complete.csv" |> ReportsGenerator.build() |> ReportsGenerator.fetch_higher_cost()
#..> {"13", 282953}
```

O usu√°rio n√∫mero `13` foi o que mais consumiu.

## Calculando a frequ√™ncia de cada tipo de pedido

Tamb√©m vamos calcular qual comida foi mais frequente. Vamos alterar nosso `report_acc` para percorrer a lista e somar de acordo com a comida. Da mesma forma que criamos um Map zerado para todos os valores, faremos para as comidas.

Ao inv√©s de fazer um `Enum` de `1..30`, vamos criar uma vari√°vel de module que √© um valor constante que vai ser usado dentro desse module.

```elixir
  def report_acc do
    foods = Enum.into(@available_foods, %{}, &{&1, 0})
    users = Enum.into(1..30, %{}, &{Integer.to_string(&1), 0})

    %{"users" => users, "foods" => foods}
  end
```

Deixamos a fun√ß√£o p√∫blica somente para ver no `iex` como ficaria:

```elixir
ReportsGenerator.report_acc()
#..> %{
#..>   "foods" => %{
#..>     "a√ßa√≠" => 0,
#..>     "churrasco" => 0,
#..>     "esfirra" => 0,
#..>     "hamb√∫rguer" => 0,
#..>     "pastel" => 0,
#..>     "pizza" => 0,
#..>     "prato_feito" => 0,
#..>     "sushi" => 0
#..>   },
#..>   "users" => %{
#..>     "1" => 0,
#..>     "10" => 0,
#..>     "11" => 0,
#..>     "12" => 0,
#..>     "13" => 0,
#..>     "14" => 0,
#..>     "15" => 0,
#..>     "16" => 0,
#..>     "17" => 0,
#..>     "18" => 0,
#..>     "19" => 0,
#..>     "2" => 0,
#..>     "20" => 0,
#..>     "21" => 0,
#..>     "22" => 0,
#..>     "23" => 0,
#..>     "24" => 0,
#..>     "25" => 0,
#..>     "26" => 0,
#..>     "27" => 0,
#..>     "28" => 0,
#..>     "29" => 0,
#..>     "3" => 0,
#..>     "30" => 0,
#..>     "4" => 0,
#..>     "5" => 0,
#..>     "6" => 0,
#..>     "7" => 0,
#..>     "8" => 0,
#..>     "9" => 0
#..>   }
#..> }
```

Agora com o acumulador contendo os dois valores, temos que alterar o `sum_values`. Vamos separar cada um dos Maps pelo pattern matching. Vamos usar cada um dos Maps isolados para atualizar o `report`. Como estamos numa linguagem imut√°vel, precisamos reatribuir o valor em `users` e em `foods`. Por fim, devolvemos o Map com os dois valores.

```elixir
  defp sum_values([id, food_name, price], %{"foods" => foods, "users" => users} = report) do
    users = Map.put(users, id, users[id] + price)
    foods = Map.put(foods, food_name, foods[food_name] + 1)

    report
    |> Map.put("users", users)
    |> Map.put("foods", foods)
  end
```

No `iex`

```elixir
"report_complete.csv" |> ReportsGenerator.build()
#..> %{
#..>   "foods" => %{
#..>     "a√ßa√≠" => 37742,
#..>     "churrasco" => 37650,
#..>     "esfirra" => 37462,
#..>     "hamb√∫rguer" => 37577,
#..>     "pastel" => 37392,
#..>     "pizza" => 37365,
#..>     "prato_feito" => 37519,
#..>     "sushi" => 37293
#..>   },
#..>   "users" => %{
#..>     "1" => 278849,
#..>     "10" => 268317,
#..>     "11" => 268877,
#..>     "12" => 276306,
#..>     "13" => 282953,
#..>     "14" => 277084,
#..>     "15" => 280105,
#..>     "16" => 271831,
#..>     "17" => 272883,
#..>     "18" => 271421,
#..>     "19" => 277720,
#..>     "2" => 271031,
#..>     "20" => 273446,
#..>     "21" => 275026,
#..>     "22" => 278025,
#..>     "23" => 276523,
#..>     "24" => 274481,
#..>     "25" => 274512,
#..>     "26" => 274199,
#..>     "27" => 278001,
#..>     "28" => 274256,
#..>     "29" => 273030,
#..>     "3" => 272250,
#..>     "30" => 275978,
#..>     "4" => 277054,
#..>     "5" => 270926,
#..>     "6" => 272053,
#..>     "7" => 273112,
#..>     "8" => 275161,
#..>     "9" => 274003
#..>   }
#..> }
```

Outra forma de atualizar o `report` com o pipe operator

```elixir
    %{report | "users" => users, "foods" => foods}
```

## Refatorando o retorno do maior pre√ßo

Nossa fun√ß√£o `fetch_higher_cost` n√£o funciona mais, pois est√° s√≥ comparando entre `users` e `food` qual √© o maior. No caso, o retorno √© de `users` e isso n√£o faz sentido.

```elixir
  def fetch_higher_cost(report, option), do: Enum.max_by(report[option], fn {_key, value} -> value end)
```

Verificando no `iex`

```elixir
"report_complete.csv" |> ReportsGenerator.build() |> ReportsGenerator.fetch_higher_cost("users")
#..> {"13", 282953}
"report_complete.csv" |> ReportsGenerator.build() |> ReportsGenerator.fetch_higher_cost("foods")
#..> {"a√ßa√≠", 37742}
```

Vamos criar uma nova vari√°vel de m√≥dulo para evitar que o usu√°rio passe uma op√ß√£o inv√°lida, pois nosso Map n√£o tem uma chave chamada `banana`.

```elixir
"report_complete.csv" |> ReportsGenerator.build() |> ReportsGenerator.fetch_higher_cost("banana")
#..> ** (Protocol.UndefinedError) protocol Enumerable not implemented for nil of type Atom. This protocol is implemented for the following type(s): HashSet, Range, Map, Function, List, Stream, Date.Range, HashDict, GenEvent.Stream, MapSet, File.Stream, IO.Stream
#..>     (elixir 1.11.4) lib/enum.ex:1: Enumerable.impl_for!/1
#..>     (elixir 1.11.4) lib/enum.ex:141: Enumerable.reduce/3
#..>     (elixir 1.11.4) lib/enum.ex:3473: Enum.reduce/3
#..>     (elixir 1.11.4) lib/enum.ex:3310: Enum.aggregate_by/4
```

[Guards](https://hexdocs.pm/elixir/guards.html) s√£o funcionalidades que podemos usar nas nossas defini√ß√µes para estender o poder do pattern matching.

```elixir
  @options ["foods", "users"]

  def fetch_higher_cost(report, option) when option in @options do
    Enum.max_by(report[option], fn {_key, value} -> value end)
  end
```

No `iex`

```elixir
"report_complete.csv" |> ReportsGenerator.build() |> ReportsGenerator.fetch_higher_cost("banana")
#..> ** (FunctionClauseError) no function clause matching in ReportsGenerator.fetch_higher_cost/2
#..>     ...
#..>     The following arguments were given to ReportsGenerator.fetch_higher_cost/2:
#..>     Attempted function clauses (showing 1 out of 1):
#..>         def fetch_higher_cost(report, option) when option === "foods" or option === "users"
#..>     (reports_generator 0.1.0) lib/reports_generator.ex:23: ReportsGenerator.fetch_higher_cost/2
```

Via pattern matching, criamos uma fun√ß√£o para quando a cl√°usula de cima n√£o for satisfeita e retornamos um erro

```elixir
  def fetch_higher_cost(report, option), do: {:error, "Invalid option!"}
```

No `iex`

```elixir
"report_complete.csv" |> ReportsGenerator.build() |> ReportsGenerator.fetch_higher_cost("banana")
#..> {:error, "Invalid option!"}
```

No caso do sucesso, vamos retornar como tupla tamb√©m

```elixir
  def fetch_higher_cost(report, option) when option in @options do
    {:ok, Enum.max_by(report[option], fn {_key, value} -> value end)}
  end
```

No `iex`

```elixir
"report_complete.csv" |> ReportsGenerator.build() |> ReportsGenerator.fetch_higher_cost("foods")
#..> {:ok, {"a√ßa√≠", 37742}}
```

## Testando nosso Parser

Criamos o arquivo `test/parser_test.exs`. Acrescentamos o `ExUnit.Case` e fazemos o `alias` do nosso m√≥dulo `ReportsGenerator.Parser`.

Em `describe` colocamos o nome da fun√ß√£o e sua aridade.

O cen√°rio que queremos testar √© para fazer o `parse` do arquivo em `test`. Utilizar sempre o arquivo de teste `report_test.csv`. Primeiro faz o teste falhar, como `banana test driven development` ü§£

```elixir
defmodule ReportsGenerator.ParserTest do
  use ExUnit.Case

  alias ReportsGenerator.Parser

  describe "parse_file/1" do
    test "parses the file" do
      file_name = "report_test.csv"

      response = Parser.parse_file(file_name)

      expected_response = "banana"

      assert response == expected_response
    end
  end
end
```

Para rodar somente o arquivo desejado

```bash
mix test test/parser_test.exs
  # 1) test parse_file/1 parses the file (ReportsGenerator.ParserTest)
    #  test/parser_test.exs:7
    #  Assertion with == failed
    #  code:  assert response == expected_response
    #  left:  #Stream<[enum: %File.Stream{line_or_bytes: :line, modes: [:raw, :read_ahead, :binary], path: "reports/report_test.csv", raw: true}, funs: [#Function<47.104660160/1 in Stream.map/2>]]>
    #  right: "banana"
    #  stacktrace:
      #  test/parser_test.exs:29: (test)
# Finished in 0.03 seconds
# 1 test, 1 failure
```

No nosso `parse` devolvemos um `Stream.map`, ou seja, n√£o tem conte√∫do nenhum pois o `Stream` √© `lazy`. S√≥ vai executar quando precisarmos do conte√∫do. Para n√£o temos que fazer um `pattern matching` com a structure esquisita dentro do Stream, vamos come√ßar com o `file_name`, passar para o `parse_file` e o resultado vamos passar para o `Enum.map` apenas imprimindo o valor dessa linha.

Ao inv√©s de usar o style com pipe na horizontal, seguir o [style guide](https://github.com/christopheradams/elixir_style_guide) e deixar o style com pipe na vertical.

```elixir
response = file_name |> Parser.parse_file() |> Enum.map(& &1)
```

```elixir
defmodule ReportsGenerator.ParserTest do
  use ExUnit.Case

  alias ReportsGenerator.Parser

  describe "parse_file/1" do
    test "parses the file" do
      file_name = "report_test.csv"

      response =
        file_name
        |> Parser.parse_file()
        |> Enum.map(& &1)

      expected_response = "banana"

      assert response == expected_response
    end
  end
end
```

Agora que usamos `Enum.map`, teremos o resultado de cada linha. Para rodar somente um teste passando o n√∫mero da linha de c√≥digo que est√°.

```bash
mix test test/parser_test.exs:7
#   1) test parse_file/1 parses the file (ReportsGenerator.ParserTest)
#      test/parser_test.exs:7
#      Assertion with == failed
#      code:  assert response == expected_response
#      left:  [["1", "pizza", 48], ["2", "a√ßa√≠", 45], ["3", "hamb√∫rguer", 31], ["4", "esfirra", 42], ["5", "hamb√∫rguer", 49], ["6", "esfirra", 18], ["7", "pizza", 27], ["8", "esfirra", 25], ["9", "churrasco", 24], ["10", "churrasco", 36]]
#      right: "banana"
#      stacktrace:
#        test/parser_test.exs:17: (test)
```

Arrumando `expected_response`:

```elixir
defmodule ReportsGenerator.ParserTest do
  use ExUnit.Case

  alias ReportsGenerator.Parser

  describe "parse_file/1" do
    test "parses the file" do
      file_name = "report_test.csv"

      response =
        file_name
        |> Parser.parse_file()
        |> Enum.map(& &1)

      expected_response = [
        ["1", "pizza", 48],
        ["2", "a√ßa√≠", 45],
        ["3", "hamb√∫rguer", 31],
        ["4", "esfirra", 42],
        ["5", "hamb√∫rguer", 49],
        ["6", "esfirra", 18],
        ["7", "pizza", 27],
        ["8", "esfirra", 25],
        ["9", "churrasco", 24],
        ["10", "churrasco", 36]
      ]

      assert response == expected_response
    end
  end
end
```

```bash
mix test test/parser_test.exs
# .

# Finished in 0.05 seconds
# 1 test, 0 failures
```

## Testando o ReportsGenerator

Vamos limpar o arquivo `test/reports_generator_test.exs` criado por padr√£o. Temos duas fun√ß√µes p√∫blicas em `ReportsGenerator` que s√£o `build` e `fetch_higher_cost`.

No `build` n√£o tem cen√°rios de borda para testar. Apenas o cen√°rio de buildar:

```elixir
defmodule ReportsGeneratorTest do
  use ExUnit.Case

  describe "build/1" do
    test "builds the report" do
      file_name = "report_test.csv"

      response = ReportsGenerator.build(file_name)

      expected_response = "banana"

      assert response == expected_response
    end
  end
end

```

```bash
mix test test/reports_generator_test.exs
  # 1) test build/1 builds the report (ReportsGeneratorTest)
  #    test/reports_generator_test.exs:5
  #    Assertion with == failed
  #    code:  assert response == expected_response
  #    left:  %{"foods" => %{"a√ßa√≠" => 1, "churrasco" => 2, "esfirra" => 3, "hamb√∫rguer" => 2, "pastel" => 0, "pizza" => 2, "prato_feito" => 0, "sushi" => 0}, "users" => %{"1" => 48, "10" => 36, "11" => 0, "12" => 0, "13" => 0, "14" => 0, "15" => 0, "16" => 0, "17" => 0, "18" => 0, "19" => 0, "2" => 45, "20" => 0, "21" => 0, "22" => 0, "23" => 0, "24" => 0, "25" => 0, "26" => 0, "27" => 0, "28" => 0, "29" => 0, "3" => 31, "30" => 0, "4" => 42, "5" => 49, "6" => 18, "7" => 27, "8" => 25, "9" => 24}}
  #    right: "banana"
```

N√£o ver como negativo ver um `expected_response` bem extenso.

```elixir
defmodule ReportsGeneratorTest do
  use ExUnit.Case

  describe "build/1" do
    test "builds the report" do
      # SETUP
      file_name = "report_test.csv"

      # EXERCISE
      response = ReportsGenerator.build(file_name)

      expected_response = %{
        "foods" => %{
          "a√ßa√≠" => 1,
          "churrasco" => 2,
          "esfirra" => 3,
          "hamb√∫rguer" => 2,
          "pastel" => 0,
          "pizza" => 2,
          "prato_feito" => 0,
          "sushi" => 0
        },
        "users" => %{
          "1" => 48,
          "10" => 36,
          "11" => 0,
          "12" => 0,
          "13" => 0,
          "14" => 0,
          "15" => 0,
          "16" => 0,
          "17" => 0,
          "18" => 0,
          "19" => 0,
          "2" => 45,
          "20" => 0,
          "21" => 0,
          "22" => 0,
          "23" => 0,
          "24" => 0,
          "25" => 0,
          "26" => 0,
          "27" => 0,
          "28" => 0,
          "29" => 0,
          "3" => 31,
          "30" => 0,
          "4" => 42,
          "5" => 49,
          "6" => 18,
          "7" => 27,
          "8" => 25,
          "9" => 24
        }
      }

      # ASSERTION
      assert response == expected_response
    end
  end
end
```

No `fetch_higher_cost` temos mais cen√°rios para testar

```elixir
  describe "fetch_higher_cost/2" do
    test "when the option is 'users', returns the user who spent the most" do
      file_name = "report_test.csv"

      response = file_name
      |> ReportsGenerator.build()
      |> ReportsGenerator.fetch_higher_cost("users")

      expected_response = "banana"

      assert response == expected_response
    end
  end
```

Executando

```bash
mix test test/reports_generator_test.exs:60
  # 1) test fetch_higher_cost/2 when the option is 'users', returns the user who spent the most (ReportsGeneratorTest)
  #    test/reports_generator_test.exs:60
  #    Assertion with == failed
  #    code:  assert response == expected_response
  #    left:  {:ok, {"5", 49}}
  #    right: "banana"
  #    stacktrace:
  #      test/reports_generator_test.exs:69: (test)
```

E arrumando o teste:

```elixir
  describe "fetch_higher_cost/2" do
    test "when the option is 'users', returns the user who spent the most" do
      file_name = "report_test.csv"

      response = file_name
      |> ReportsGenerator.build()
      |> ReportsGenerator.fetch_higher_cost("users")

      expected_response = {:ok, {"5", 49}}

      assert response == expected_response
    end
  end
```

Vamos fazer para `foods` e para uma op√ß√£o inv√°lida

```elixir
defmodule ReportsGeneratorTest do
  use ExUnit.Case

  describe "build/1" do
    test "builds the report" do
      file_name = "report_test.csv"

      response = ReportsGenerator.build(file_name)

      expected_response = %{
        "foods" => %{
          "a√ßa√≠" => 1,
          "churrasco" => 2,
          "esfirra" => 3,
          "hamb√∫rguer" => 2,
          "pastel" => 0,
          "pizza" => 2,
          "prato_feito" => 0,
          "sushi" => 0
        },
        "users" => %{
          "1" => 48,
          "10" => 36,
          "11" => 0,
          "12" => 0,
          "13" => 0,
          "14" => 0,
          "15" => 0,
          "16" => 0,
          "17" => 0,
          "18" => 0,
          "19" => 0,
          "2" => 45,
          "20" => 0,
          "21" => 0,
          "22" => 0,
          "23" => 0,
          "24" => 0,
          "25" => 0,
          "26" => 0,
          "27" => 0,
          "28" => 0,
          "29" => 0,
          "3" => 31,
          "30" => 0,
          "4" => 42,
          "5" => 49,
          "6" => 18,
          "7" => 27,
          "8" => 25,
          "9" => 24
        }
      }

      assert response == expected_response
    end
  end

  describe "fetch_higher_cost/2" do
    test "when the option is 'users', returns the user who spent the most" do
      file_name = "report_test.csv"

      response = file_name
      |> ReportsGenerator.build()
      |> ReportsGenerator.fetch_higher_cost("users")

      expected_response = {:ok, {"5", 49}}

      assert response == expected_response
    end

    test "when the option is 'foods', returns the most consumed food" do
      file_name = "report_test.csv"

      response = file_name
      |> ReportsGenerator.build()
      |> ReportsGenerator.fetch_higher_cost("foods")

      expected_response = {:ok, {"esfirra", 3}}

      assert response == expected_response
    end

    test "when the as invalid option is given, returns an error" do
      file_name = "report_test.csv"

      response = file_name
      |> ReportsGenerator.build()
      |> ReportsGenerator.fetch_higher_cost("bananas")

      expected_response = {:error, "Invalid option!"}

      assert response == expected_response
    end
  end
end
```

O `ExUnit` roda todos os testes em paralelo, o que faz com que a switch seja muito r√°pida mesmo que tenha muitos testes.

```bash
mix test
# .....

# Finished in 0.07 seconds
# 5 tests, 0 failures
```

---

## Medindo o tempo de execu√ß√£o

Medimos o tempo de execu√ß√£o no Elixir pelo m√≥dulo `timer` do Erlang. No `iex` podemos ver as fun√ß√µes digitando `:timer.` e a tecla `tab`:

```elixir
:timer.
#..> apply_after/4       apply_interval/4    cancel/1
#..> code_change/3       exit_after/2        exit_after/3
#..> get_status/0        handle_call/3       handle_cast/2
#..> handle_info/2       hms/3               hours/1
#..> init/1              kill_after/1        kill_after/2
#..> minutes/1           now_diff/2          seconds/1
#..> send_after/2        send_after/3        send_interval/2
#..> send_interval/3     sleep/1             start/0
#..> start_link/0        tc/1                tc/2
#..> tc/3                terminate/2
```

Iremos usar a fun√ß√£o [tc/1](https://erlang.org/doc/man/timer.html#tc-1) com aridade 1, que espera uma fun√ß√£o an√¥nima com a fun√ß√£o em si que queremos executar para medir o tempo de execu√ß√£o.

Vamos executar no `iex`:

```elixir
:timer.tc(fn -> ReportsGenerator.build("report_complete.csv") end)
#..>{2508696,
#..> %{
#..>   "foods" => %{
#..>     "a√ßa√≠" => 37742,
#..>     "churrasco" => 37650,
#..>     "esfirra" => 37462,
#..>     "hamb√∫rguer" => 37577,
#..>     "pastel" => 37392,
#..>     "pizza" => 37365,
#..>     "prato_feito" => 37519,
#..>     "sushi" => 37293
#..>   },
#..>   "users" => %{
#..>     "1" => 278849,
#..>     "10" => 268317,
#..>     "11" => 268877,
#..>     "12" => 276306,
#..>     "13" => 282953,
#..>     "14" => 277084,
#..>     "15" => 280105,
#..>     "16" => 271831,
#..>     "17" => 272883,
#..>     "18" => 271421,
#..>     "19" => 277720,
#..>     "2" => 271031,
#..>     "20" => 273446,
#..>     "21" => 275026,
#..>     "22" => 278025,
#..>     "23" => 276523,
#..>     "24" => 274481,
#..>     "25" => 274512,
#..>     "26" => 274199,
#..>     "27" => 278001,
#..>     "28" => 274256,
#..>     "29" => 273030,
#..>     "3" => 272250,
#..>     "30" => 275978,
#..>     "4" => 277054,
#..>     "5" => 270926,
#..>     "6" => 272053,
#..>     "7" => 273112,
#..>     "8" => 275161,
#..>     "9" => 274003
#..>   }
#..> }}
```

onde o primeiro elemento da tupla (2508696) √© o tempo que levou a execu√ß√£o em microssegundos.

Ao inv√©s de consumir o `report_complete.csv`, vamos consumir de forma paralela concorrente os tr√™s arquivos e agregar o resultado num import s√≥.
